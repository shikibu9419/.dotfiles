#!/bin/zsh

source $DOTPATH/bin/_shared_functions

main() {
  detect::check

  if [ $1 = '-c' ]; then
    confirm 'Do you really want to clear gtags files?' && rm $(git rev-parse --show-cdup)(GPATH|GTAGS|GRTAGS)
    exit 0
  elif [ -f $1 ]; then
    func=detect::detect_on_file
  elif [ -d $1 ]; then
    func=detect::detect_in_directory
  else
    func=detect::detect_definition
  fi

  $func $@
}

detect::check() {
  check-git-available
  # TODO: to _shared_functions
  has 'fzf'    || error 'fatal: fzf is not installed.'
  has 'global' || error 'fatal: global is not installed.'

  if ! $(global -u); then
    confirm 'Create GTAGS?' && gtags -v $(git rev-parse --show-cdup) || error 'Detection aborted.'
    echo
  fi
}

detect::detect_on_file() {
  list=$(global -fx $1 | awk '{ print $1 " - " $2 }')

  [ -z $list ] && error "$1: Nothing is defined."

  echo $list | fzf --ansi --prompt="$1> " \
    --bind "ctrl-l:execute: nvim \$(echo {} | awk '{ print \"+\" \$3 }') $1" \
    --preview="echo {} |
      awk '{ start = \$3 - 10; end = \$3 + 10; if(gsub(/\?/,\"\\\\\\\\?\",\$1)) target = \$1; else target = \$1 }
           { if(start < 1) start = 1 }
           { print \"highlight -O ansi $1 | sed -n \" start \",\" end \"p | grep --color=always -10 \" target }' |
      bash"
}

detect::detect_in_directory() {
  list=$(git ls-files $1)

  [ -z $list ] && error "$1: There is no file."

  echo $list | fzf --ansi --prompt="$1> " \
    --bind "ctrl-m:execute: nvim {}" \
    --preview="highlight --force -O ansi {}"
}

detect::detect_definition() {
  label=-x
  target=$1
  case $target in
    -[dgr])
      label=$1
      target=$2
      ;;
    -*)
      error "There is no such option: $target"
  esac

  [[ $label =~ -[dx] ]] &&  defs=$(global -dx $target | awk '{ print $3 " - " $2 }')
  [[ $label =~ -[rx] ]] &&  refs=$(global -rx $target | awk '{ print $3 " - " $2 }')
  [[ $label =  -g    ]] && greps=$(global -gx $target | awk '{ print $3 " - " $2 }')
  list=$({ echo $defs; echo $refs; echo $greps } | sed '/^$/d')

  [ -z $list ] && error "$target: Not found."

  echo $list | fzf --ansi --prompt="$target> " \
    --bind "ctrl-l:execute: nvim \$(echo {} | awk '{ print \"+\" \$3 \" \" \$1 }')" \
    --preview="echo {} |
      awk '{ start = \$3 - 10; end = \$3 + 10 }
           { if(start < 1) start = 1 }
           { print \"highlight --force -O ansi \" \$1 \" | sed -n \" start \",\" end \"p | grep --color=always -10 $target\" }' |
      bash"
}

main $@
