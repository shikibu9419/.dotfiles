#!/bin/zsh

source $DOTPATH/bin/_shared_functions
HIGHLIGHT='highlight --force -O ansi'

main() {
  detect::check

  if [ $1 = '-c' ]; then
    confirm 'Do you really want to clear gtags files?' && rm $(git rev-parse --show-cdup)(GPATH|GTAGS|GRTAGS)
  elif [ -f $1 ]; then
    detect::detect_on_file $@
  elif [ -d $1 ]; then
    detect::detect_in_directory $@
  else
    detect::detect_definition $@
  fi
}

detect::check() {
  check-git-available
  check-availables 'fzf' 'global'

  if ! $(global -u); then
    confirm 'Create GTAGS?' && gtags -v $(git rev-parse --show-cdup) || error 'Detection aborted.'
    echo
  fi
}

detect::detect_on_file() {
  list=$(global -fx $1 | awk '{ print $1 " - " $2 }')

  [ -z $list ] && error "$1: Nothing is defined."

  detect::call_fzf "$list" "$1" '{1}'
}

detect::detect_in_directory() {
  list=$(git ls-files $1)

  [ -z $list ] && error "$1: There is no file."

  echo $list | fzf -m --ansi --prompt="$1> " \
    --bind "ctrl-m:execute: nvim {}" \
    --preview="$HIGHLIGHT {}"
}

detect::detect_definition() {
  label=-x
  content=$1
  case $content in
    -[dgr])
      label=$1
      content=$2
      ;;
    -*)
      error "There is no such option: $content"
  esac

  [[ $label =~ -[dx] ]] &&  defs=$(global -dx $content | awk '{ print $3 " - " $2 }')
  [[ $label =~ -[rx] ]] &&  refs=$(global -rx $content | awk '{ print $3 " - " $2 }')
  [[ $label =  -g    ]] && greps=$(global -gx $content | awk '{ print $3 " - " $2 }')
  list=$({ echo $defs; echo $refs; echo $greps } | sed '/^$/d')

  [ -z $list ] && error "$content: Not found."

  detect::call_fzf "$list" '{1}' "$content"
}

detect::call_fzf() {
  list=$1
  target=$2
  content=$3

  echo $list | fzf -m --ansi --prompt="$content> " \
    --bind "ctrl-l:execute: (set {}; $HIGHLIGHT $target | less +1 -NR -p $content)" \
    --preview="set {}; \
               line=\$(cont=$content; $HIGHLIGHT $target | sed -n \${3}p | grep --color=always \${cont/\?/\\\\?}); \
               $HIGHLIGHT $target | sed -E '{3}'\"s/.*/\$line/\""
}

main $@
